TITLE: Example 1 - Model with 3 triplets measuring 3 traits
DATA: FILE IS triplets.dat;

VARIABLE: 
      NAMES = 
      i1i2 i1i3 i2i3 
      i4i5 i4i6 i5i6 
      i7i8 i7i9 i8i9
      i10i11 i10i12 i11i12; 
      CATEGORICAL = i1i2-i11i12;

ANALYSIS:
ESTIMATOR=ulsmv;  PARAMETERIZATION=THETA;

MODEL: 
   Trait1  BY  	
	i1i2*1  i1i3*1  (L1)
	i4i5*-1  i4i6*-1  (L4)
	i7i8*1  i7i9*1  (L7)
 	i10i11*1  i10i12*1  (L10);
   Trait2  BY  	
	i1i2*-1  (L2_n)
	i2i3*1  (L2)
	i4i5*-1  (L5_n)
	i5i6*1  (L5)
	i7i8*-1  (L8_n)
	i8i9*1  (L8)
    i10i11*1  (L11_n)
    i11i12*-1  (L11);
   Trait3  BY  	
	i1i3*-1  i2i3*-1  (L3_n)
	i4i6*-1  i5i6*-1  (L6_n)
	i7i9*1  i8i9*1  (L9_n)
    i10i12*-1  i11i12*-1  (L12_n);

Trait1-Trait3@1      ! variances for all traits are set to 1

! optional - starting values for correlations between traits	
Trait1 WITH Trait2*-0.4  Trait3*0;
Trait2 WITH Trait3*0.3;

! declare uniquenesses 
i1i2*2 (e1e2);	 
i1i3*2 (e1e3);	  
i2i3*2 (e2e3);
i4i5*2 (e4e5);	 
i4i6*2 (e4e6);	  
i5i6*2 (e5e6);
i7i8*2 (e7e8);	 
i7i9*2 (e7e9);	  
i8i9*2 (e8e9);
i10i11*2 (e10e11);  
i10i12*2 (e10e12);  
i11i12*2 (e11e12);

! declare correlated uniqunesses and set their starting values
i1i2 WITH i1i3*1 (e1);
i1i2 WITH i2i3*-1 (e2_n); 
i1i3 WITH i2i3*1 (e3);
i4i5 WITH i4i6*1 (e4); 
i4i5 WITH i5i6*-1 (e5_n);
i4i6 WITH i5i6*1 (e6);
i7i8 WITH i7i9*1 (e7); 
i7i8 WITH i8i9*-1 (e8_n);
i7i9 WITH i8i9*1 (e9);
i10i11 WITH i10i12*1 (e10);	  
i10i11 WITH i11i12*-1 (e11_n);  
i10i12 WITH i11i12*1 (e12);

MODEL CONSTRAINT:
!factor loadings relating to the same item are equal in absolute value
L2_n = -L2;	L5_n = -L5;	L8_n = -L8;	L11_n = -L11;

! pair's uniqueness is equal to sum of 2 utility uniqunesses	
e1e2 = e1 - e2_n; 
e1e3 = e1 + e3;
e2e3 = -e2_n + e3;
e4e5 = e4 - e5_n;
e4e6 = e4 + e6;
e5e6 = -e5_n + e6;
e7e8 = e7 - e8_n;
e7e9 = e7 + e9;
e8e9 = -e8_n + e9;
e10e11 = e10 - e11_n;
e10e12 = e10 + e12;
e11e12 = -e11_n + e12;

! fix one uniqueness per block for identification
e1=1; 	e4=1;	e7=1; 	e10=1;
